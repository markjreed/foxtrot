; Targeting Commodore computers that have been upgraded with an 816
; (e.g. C64/128 with a SuperCPU, or the Commander X16)
.include "cbm_kernal.inc"

; load macros for switching modes and register widths
.macpack cpu65816

.segment "ZEROPAGE"
ptr:             .res 2

.segment "RODATA"
msg: .byte $0e,"I will not throw paper airplanes in class.",$0d,0

; make sure the null-terminator is a whole word regardless of string length
     .word 0

.segment "CODE"
main:
                sei
                native_mode  ; 65816 starts up in 6502 emulation mode
                ai16         ; and even in native mode defaults to 8-bit regs
                cli

                lda #msg
                sta ptr

                ldx #500
loop:           ldy #0
putc:           lda (ptr),y
                beq done
                jsr nchrout
                iny
                iny
                bra putc
done:           dex
                bne loop
                sei
                emulation_mode
                cli
                rts

; CBM KERNAL only works in emulation mode; provide native-mode wrapper
nchrout:        phx         ; switching modes zeroes high byte of index regs,
                phy         ; so save them on the stack
                ai8
                jsr CHROUT
                xba
                beq @skip
                jsr CHROUT
@skip:          ai16
                ply
                plx
                rts
